options {
  STATIC=false;
}

PARSER_BEGIN(Parser)

package parser;
import ast.*;import ast.bools.*;import ast.ints.*;
import java.util.List;
import java.util.ArrayList;

public class Parser {

}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
}

TOKEN :
{
  < Num: (["0"-"9"]) + >
  |
  < PLUS : "+" >
  |
  < MINUS : "-">
  |
  < TIMES : "*">
  |
  < DIV : "/">
  |
  < LPAR : "(" >
  |
  < RPAR : ")" >
  |
  < EL: "\n" >
  |
  < FALSE: "false" >
  |
  < TRUE: "true" >
  |
  < LET: "let" >
  |
  < IN: "in" >
  |
  < NEW: "new" >
  |
  < ASSIGN: ":=" >
  |
  < DEREF: "!" >
  |
  < IF: "if" >
  |
  < ELSE: "else" >
  |
  < WHILE: "while" >
  |
  < DO: "do" >
  |
  < END: "end" >
  |
  < PRINT: "print" | "println" >
  |
  < Id: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9","_"] )* >
}

TOKEN :
{
  < AND: "&&" >
  |
  < OR: "||" >
  |
  < NOT: "~" >
  |
  < LESS: "<" >
  |
  < GREATER: ">" >
  |
  < LESSEQ: "<=" >
  |
  < GREATEREQ: ">=" >
  |
  < EQ : "=" >
  |
  < EQUALS: "==" >
  |
  < NOTEQUALS: "!=" >
}


Exp Start() :
{ Exp e; }
{
  e = SeqExp() <EL> { return e; }
}

Exp SeqExp() :
{ Exp e1, e2;}
{
    e1 = Decl()
    ( ";" e2 = SeqExp() { e1 = new ASTSeq(e1, e2); } )?
        { return e1; }
}

Exp Decl() :
{ List<ASTVarDecl> varDecls = new ArrayList<ASTVarDecl>(); Exp e; ASTVarDecl varDecl; }
{
    <LET> (varDecl = LetAssignment() { varDecls.add(varDecl); })+ <IN> [<EL>] e = SeqExp() { return new ASTLet(varDecls, e); }
  |
    e = BExp() { return e; }
  |
    e = WhileDo() { return e; }
  |
    e = IfElse() { return e; }
  |
    e = Print() { return e; }
}

Exp Print() :
{ Exp e;}
{
    <PRINT> e = Decl() {return new ASTPrint(e);}
}

Exp IfElse() :
{ Exp e1, e2, e3 = null; }
{
  <IF> <LPAR> e1 = BExp() <RPAR> [<EL>] e2 = SeqExp() [<EL>] (<ELSE> [<EL>] e3 = SeqExp() [<EL>])? <END> { return new ASTIf(e1, e2, e3); }
}

Exp WhileDo():
{Exp e1, e2;}
{
  <WHILE> e1 = BExp() <DO> e2 = SeqExp() <END> { return new ASTWhile(e1, e2); }
}

ASTVarDecl LetAssignment() :
{ Token x; Exp e; }
{
  x = <Id> <EQ> e = BExp() [<EL>] { return new ASTVarDecl(x.image, e); }
}

Exp BExp() :
{ Exp e1, e2; }
{
    e1 = Assignment()
    [ <AND> e2 = BExp() { e1 = new ASTAnd(e1,e2); }
    | <OR> e2 = BExp() { e1 = new ASTOr(e1,e2); }
    | <NOT> e2 = BExp() { e1 = new ASTNot(e2); }
     ]
    { return e1; }

}

Exp Assignment() :
{ Exp e1, e2; }
{
    e1 = Cmp()
    [ <ASSIGN> e2 = Assignment() { e1 = new ASTAssign(e1, e2); } ]
    { return e1; }
}

Exp Cmp() :
{ Exp e1, e2; }
{
   e1 = Expr()
   ( <EQUALS> e2 = Expr() { e1 = new ASTEquals(e1, e2); }
     | <NOTEQUALS> e2 = Expr() { e1 = new ASTNotEquals(e1, e2); }
     | <LESS> e2 = Expr() { e1 = new ASTLess(e1, e2); }
     | <GREATER> e2 = Expr() { e1 = new ASTGreater(e1, e2); }
     | <LESSEQ> e2 = Expr() { e1 = new ASTLessEq(e1, e2); }
     | <GREATEREQ> e2 = Expr() { e1 = new ASTGreaterEq(e1, e2); }
   )*
   { return e1; }
}

Exp Expr() :
{ Exp e1, e2; }
{
   e1 = Term() 
     ( <PLUS> e2 = Term() { e1 = new ASTAdd(e1,e2); }
     | <MINUS> e2 = Term() { e1 = new ASTSub(e1,e2); } )*
     { return e1; }
}

Exp Term() :
{ Exp e1, e2; }
{
     e1 = Fact()
     ( <TIMES> e2 = Fact() { e1 = new ASTMult(e1,e2); }
     | <DIV> e2 = Fact() { e1 = new ASTDiv(e1,e2); } )*
     { return e1; }
}

Exp Fact() :
{ Token x; Exp e; }
{
      x = <Num>
  {
  return new ASTInt(Integer.parseInt(x.image));
}
  |
      <MINUS> e = Fact()
  {
      return new ASTMult(new ASTInt(-1), e);
  }
  |
      x = <TRUE> { return new ASTBool(true); }
  |
      x = <FALSE> { return new ASTBool(false); }
  |
      <LPAR> e = Decl() <RPAR> { return e; }
  |
      <NOT> e = Fact() { return new ASTNot(e); }
  |
      x = <Id> { return new ASTId(x.image); }
  |
      <NEW> e = Fact() { return new ASTNew(e); }
  |
      <DEREF> e = Fact() { return new ASTDeref(e); }
  //|
    //  <LPAR> <RPAR> { return new ASTUnit(); }
}



